plugins {
    id 'java-library'
    id 'kotlin'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id "org.graalvm.plugin.compiler" version "0.1.0-alpha2"
}

ext.version_graal = '21.2.0'

graal {
    version "$version_graal"
}

compileJava {
    sourceCompatibility = 1.8
}

dependencies {
    implementation project(':core:imposter-engine')
    implementation project(':scripting:scripting-common')

    pluginImplementation "org.graalvm.js:js:$version_graal"
    pluginImplementation "org.graalvm.truffle:truffle-api:$version_graal"
    pluginImplementation "org.graalvm.sdk:graal-sdk:$version_graal"

    // test
    testImplementation project(':imposter-test')
    testImplementation "com.jayway.restassured:rest-assured:$version_restAssured"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

shadowJar {
    // override plugin archive name
    archiveBaseName.set("imposter-plugin-js-graal")
    archiveVersion.set('')
    archiveClassifier.set('')
    configurations = [project.configurations.pluginImplementation]
}

task dist {
    dependsOn shadowJar
}
