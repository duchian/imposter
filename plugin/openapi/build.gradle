apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

ext {
    version_aws_sdk = '1.11.1000'
    version_swagger_parser = '2.0.25'
    version_swagger_request_validator = '2.18.0'
}

compileJava {
    sourceCompatibility = 1.8
}

dependencies {
    implementation project(':imposter-core')
    implementation "io.swagger.parser.v3:swagger-parser:$version_swagger_parser"
    implementation "com.atlassian.oai:swagger-request-validator-core:$version_swagger_request_validator"

    implementation platform("com.amazonaws:aws-java-sdk-bom:$version_aws_sdk")
    implementation 'com.amazonaws:aws-java-sdk-s3'

    // test
    testImplementation  project(':imposter-test')
    testImplementation  "com.jayway.restassured:rest-assured:$version_restAssured"
    testImplementation  "org.testcontainers:testcontainers:$version_testcontainers"
    testImplementation  "com.adobe.testing:s3mock-testcontainers:2.2.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}
