apply plugin: 'groovy'
apply plugin: 'maven-publish'

ext {
    version_swaggerParser = '2.0.25'
}

compileJava {
    sourceCompatibility = 1.8
}

task copyStaticUi(type: Copy) {
    from 'src/main/html'
    into "${sourceSets.main.output.resourcesDir}/swagger-ui"
}

tasks.processResources.dependsOn copyStaticUi

dependencies {
    implementation project(':imposter-core')
    implementation "io.swagger.parser.v3:swagger-parser:$version_swaggerParser"
    implementation group: 'com.atlassian.oai', name: 'swagger-request-validator-core', version: '2.18.0'

    // test
    testCompile project(':imposter-test')
    testCompile "com.jayway.restassured:rest-assured:$version_restAssured"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}
